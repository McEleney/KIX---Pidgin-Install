;Pidgin installer KIX script
;Copyright 2011 McEleney Motors <john.rogers@mceleney.com>
;
;The following functions taken from http://www.kixtart.org/udf/
;Why reinvent the wheel?
Function ReadFile($file)
	Dim $lf, $f, $_, $t
	$lf=chr(10)
	$f=freefilehandle
	$_=open($f,$file)
		if @error   exit @error   endif
		do $t=$t+$lf+readline($f) until @error
		$_=close($f)
	$ReadFile=split(substr($t,2),$lf)
EndFunction

Function WriteFile($file,$array)
	Dim $, $f, $line
	If Not VarType($Array) & 8192    Exit(1)        EndIf
	$f=freefilehandle
	$=open($f,$file,5)
		if @error   exit @error   endif
		for each $line in $array
			$=writeline($f,$line+@crlf)
			if @error   exit @error   endif
		next
		$=close($f)
EndFunction

Function Replace($String,$SS,$RS)
	$Replace=Split($String,$SS)
	$Replace=Join($Replace,$RS)
EndFunction

;Path to Pidgin or Pidgin-offline install file
$INSTALLER = "\\SERVER\installs\pidgin\pidgin-offline.exe"
If Not Exist($INSTALLER)
	? $INSTALLER + " does not exist! Aborting!"
	Exit
EndIf
$pVersion = GetFileVersion("\\SERVER\installs\pidgin\pidgin-offline.exe","ProductVersion")
$PROGRAMFILES = EXPANDENVIRONMENTVARS(%PROGRAMFILES%)
$SYSTEMROOT = EXPANDENVIRONMENTVARS(%SYSTEMROOT%)
$APPDATA = EXPANDENVIRONMENTVARS(%APPDATA%)

$IPFirstByte = Left(@IPAddress0,3)
If Not $IPFirstByte = "198"
	? "Not directly connected to the dealership network, aborting!"
	Exit
EndIf
If Not Exist("$APPDATA\.purple\accounts.xml")
	? "No accounts.xml file, creating one..."
	$password = Split(@FULLNAME, " ")
	$username = Replace(@FULLNAME, " ", ".")
	$password = $password[1] + "1"
	$password = Replace($password,"a","4")
	$password = Replace($password,"e","3")
	$password = Replace($password,"i","1")
	$password = Replace($password,"o","0")
	$password = Replace($password,"u","7")
	$accountfile = ReadFile('\\SERVER\installs\pidgin\accounttemplate')
	$i = 0
	For Each $line In $accountfile
		$line = Replace($line,'!USERNAME!',$username)
		$line = Replace($line,'!PASSWORD!',$password)
		$line = Replace($line,'!FULLNAME!',@FULLNAME)
		$accountfile[$i] = $line
		$i = $i + 1
	Next
	If Not Exist("$APPDATA\.purple")
		MD "$APPDATA\.purple"
	EndIf
	WriteFile("$APPDATA\.purple\accounts.xml",$accountfile)
	If Exist("$APPDATA\.purple\accounts.xml")
		? "Wrote default accounts.xml file!"
	Else
		? "Couldn't create accounts.xml, aborting!"
		Exit
	EndIf
Else
	? "Found accounts.xml file!"
EndIf
If Not Exist("$APPDATA\.purple\certificates\x509\tls_peers\mceleneychat")
	? "Copying certificates..."
	Copy "\\SERVER\installs\pidgin\certificates\" "$APPDATA\.purple\certificates\" /s
EndIf
If Exist("$PROGRAMFILES\Pidgin\Pidgin.exe") 
	? "Pidgin is installed already!"
	? "Checking if upgrade is available..."
	$cVersion = GetFileVersion("$PROGRAMFILES\Pidgin\Pidgin.exe")
	? "Current version:   $cVersion"
	? "Available version: $pVersion"
	If Not $cVersion = $pVersion
		? "Upgrading..."
		Shell("$SYSTEMROOT\system32\taskkill.exe /F /IM pidgin.exe")
		DelValue("HKEY_CURRENT_USER\Software\Microsoft\Windows\CurrentVersion\Run","Pidgin")
		Shell("\\SERVER\installs\pidgin\pidgin-offline.exe /S")	
		WriteValue("HKEY_CURRENT_USER\Software\Microsoft\Windows\CurrentVersion\Run","Pidgin","$PROGRAMFILES\Pidgin\Pidgin.exe","REG_SZ")
		? "Done!"
	EndIf
Else
	? "Pidgin is not installed!"
	? "Installing Pidgin in silent mode..."
	Shell("\\SERVER\installs\pidgin\pidgin-offline.exe /S /DS=1 /SMS=1")
	? "Setting Pidgin to run at startup..."
	WriteValue("HKEY_CURRENT_USER\Software\Microsoft\Windows\CurrentVersion\Run","Pidgin","$PROGRAMFILES\Pidgin\Pidgin.exe","REG_SZ")
	? "Done!"
EndIf

;Check for spellcheck files and copy if they're not there
If Not Exist("$PROGRAMFILES\Pidgin\spellcheck\share\enchant\myspell\en_US.dic")
	If Not Exist("$PROGRAMFILES\Pidgin\spellcheck\share\enchant\myspell")
		MD "$PROGRAMFILES\Pidgin\spellcheck\share"
		MD "$PROGRAMFILES\Pidgin\spellcheck\share\enchant"
		MD "$PROGRAMFILES\Pidgin\spellcheck\share\enchant\myspell"
	EndIf
	? "Copying spellcheck dictionaries..."
	Copy "\\SERVER\installs\pidgin\en_US\*" "$PROGRAMFILES\Pidgin\spellcheck\share\enchant\myspell\"
EndIf
